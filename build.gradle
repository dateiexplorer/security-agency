plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

group 'de.dhbw.mosbach.msa'
version '1.0'

repositories {
    mavenCentral()
}

mainClassName='de.dhbw.mosbach.msa.Launcher'

javafx {
    version = "15.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    implementation 'com.google.guava:guava:30.1-jre'
    implementation group: 'org.hsqldb', name: 'hsqldb', version: '2.5.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:linux"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac"
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls,javafx.fxml'
        ]
    }
}

run {
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls,javafx.fxml'
        ]
    }
}

jar {
    manifest {
        attributes('Main-Class' : 'de.dhbw.mosbach.msa.Launcher')
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}